<!DOCTYPE html>
<!-- saved from url=(0069)http://cgi.cse.unsw.edu.au/~cs2041cgi/16s2/answers.py?tut/week07.html -->
<html lang="en"><head><meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<title>Week 07 Tutorial</title>
<link href="./Week 07 Tutorial_files/2041.css" rel="stylesheet">
</head>
<body data-shoptagr-mini="yes">
<div class="container">
<div id="heading"><a class="heading_link" target="_blank" id="left_heading_link" href="http://www.cse.unsw.edu.au/~cs2041/16s2/">COMP[29]041 16s2</a>
We love perl
<a class="heading_link" target="_blank" id="right_heading_link" href="http://www.cse.unsw.edu.au/~cs2041/16s2/">Software Construction</a></div>
<div class="tutlab tut">
<ol>
<li>

Write a Perl program <code>word_frequency.pl</code> which prints a count of all the words found in its
input.  Your program should ignore case. It should treat any sequence of alphabetic characters([a-z])
as a word. It should treat any non-alphabetic character as a space. It should print words and their
counts sorted in increasing order of frequency in format shown in this example:


<pre class="command_line"><kbd class="shell">./word_frequency.pl
</kbd><kbd>Peter Piper picked a peck of pickled peppers;
A peck of pickled peppers Peter Piper picked;
If Peter Piper picked a peck of pickled peppers,
Where's the peck of pickled peppers Peter Piper picked?
<span class="eof"></span></kbd>
<samp>1 the
1 s
1 where
1 if
3 a
4 pickled
4 piper
4 peppers
4 of
4 peter
4 peck
4 picked
</samp></pre>
<div class="answer">
Sample solution for word_frequency.pl


<pre class="program">#!/usr/bin/perl -w

while ($line = &lt;&gt;) {
    $line =~ tr/A-Z/a-z/;
    foreach $word ($line =~ /[a-z]+/g) {
        $count{$word}++;
    }
}
@words = keys %count;
@sorted_words = sort {$count{$a} &lt;=&gt; $count{$b}} @words;
foreach $word (@sorted_words) {
    printf "%d %s\n", $count{$word}, $word;
}
</pre>

Python solution


<pre class="program">#!/usr/bin/python
import fileinput,re, collections

count = collections.defaultdict(int)
for line in fileinput.input():
    for word in re.findall(r'\w+', line.lower()):
        count[word] += 1
words = list(count.keys())
sorted_words = sorted(words,  key=lambda w: count[w])
for word in sorted_words:
    print("%d %s" % (count[word], word))
</pre>
</div>
</li><li>

Write a Perl program <code>missing_words.pl</code> which given two files as arguments prints,
in sorted order, all the words found in file1 but not file2.
<p>
You can assume words occur one per line in each file.

</p><div class="answer">
Straight-forward sample solution for missing_words.pl


<pre class="program">#!/usr/bin/perl -w
# print words in file 1 but not file 2

die "Usage: $0 &lt;file1&gt; &lt;file2&gt;\n" if @ARGV != 2;

open(F, "$ARGV[0]") || die("Can't open $ARGV[0]: $!");
while ($word = &lt;F&gt;) {
    chomp($word);
    $w{$word} = "added";
}
close(F);

open(F, "$ARGV[1]") || die("Can't open $ARGV[1]: $!");
while ($word = &lt;F&gt;) {
    chomp($word);
    $w{$word} = "deleted";
}
close(F);

foreach $word (sort keys %w) {
    print "$word\n" if $w{$word} ne "deleted";
}
</pre>

Concise but less obvious sample solution for missing_words.pl


<pre class="program">#!/usr/bin/perl -w
# print words in file 1 but not file 2

die "Usage: $0 &lt;file1&gt; &lt;file2&gt;\n" if @ARGV != 2;

open(F, "$ARGV[0]") || die("Can't open $ARGV[0]: $!");
$w{$_}++ while &lt;F&gt;;
close(F);

open(F, "$ARGV[1]") || die("Can't open $ARGV[1]: $!");
delete $w{$_} while &lt;F&gt;;
close(F);

print sort keys %w;
</pre>
</div>
<p></p></li><li>
Write a Perl program that given the road distances between
a number of towns (on standard input) calculates the shortest journey
between two towns specified as arguments.
Here is an example of how your program should behave.


<pre class="command_line"><kbd class="shell">./shortest_path.pl Parkes Gilgandra
</kbd><kbd>Bourke Broken-Hill   217
Bourke Dubbo          23
Bourke Gilgandra      62
Bourke Parkes         71
Canowindra Dubbo      35
Canowindra Gilgandra  13
Canowindra Parkes    112
Dubbo Gilgandra       91
Dubbo Parkes          57
<span class="eof"></span></kbd>
<samp>Shortest route is length = 105: Parkes Dubbo Canowindra Gilgandra.
</samp></pre>
<div class="answer">

Perl sample solution storing set as array


<pre class="program">#!/usr/bin/perl -w
# find shortest path between two towns

die "Usage: $0 &lt;start&gt; &lt;finish&gt;\n" if @ARGV != 2;
$start = $ARGV[0];
$finish = $ARGV[1];

while (&lt;STDIN&gt;) {
    /(\w+)\s+(\w+)\s+(\d+)/ || next;
    $distance{$1}{$2} = $3;
    $distance{$2}{$1} = $3;
}

$shortest_journey{$start} = 0;
$route{$start} = "";
@unprocessed_towns = keys %distance;
$current_town = $start;
while ($current_town &amp;&amp; $current_town ne $finish) {
    @unprocessed_towns = grep(!/^$current_town$/, @unprocessed_towns); # inefficient
    foreach  $town (@unprocessed_towns) {
        next if !defined $distance{$current_town}{$town};
        my $d = $shortest_journey{$current_town} + $distance{$current_town}{$town};
        next if defined $shortest_journey{$town} &amp;&amp; $shortest_journey{$town} &lt; $d;
        $shortest_journey{$town} = $d;
        $route{$town} = "$route{$current_town} $current_town";
    }
    my $min_distance = 1e99;   # must be larger than any possible distance
    $current_town = "";
    foreach $town (@unprocessed_towns) {
        next if !defined $shortest_journey{$town};
        next if $shortest_journey{$town} &gt; $min_distance ;
        $min_distance = $shortest_journey{$town};
        $current_town = $town;
    }
}

if (!defined $shortest_journey{$finish}) {
    print "No route from $start to $finish.\n";
} else {
    print "Shortest route is length = $shortest_journey{$finish}:$route{$finish} $finish.\n";
}
</pre>

Perl sample solution storing set as hash


<pre class="program">#!/usr/bin/perl -w
# find shortest path between two towns

die "Usage: $0 &lt;start&gt; &lt;finish&gt;\n" if @ARGV != 2;
$start = $ARGV[0];
$finish = $ARGV[1];

while (&lt;STDIN&gt;) {
    /(\w+)\s+(\w+)\s+(\d+)/ || next;
    $distance{$1}{$2} = $3;
    $distance{$2}{$1} = $3;
}

$shortest_journey{$start} = 0;
$route{$start} = "";
%unprocessed_towns = %distance;
$current_town = $start;
while ($current_town &amp;&amp; $current_town ne $finish) {
    delete $unprocessed_towns{$current_town};
    foreach  $town (keys %unprocessed_towns) {
        next if !defined $distance{$current_town}{$town};
        my $d = $shortest_journey{$current_town} + $distance{$current_town}{$town};
        next if defined $shortest_journey{$town} &amp;&amp; $shortest_journey{$town} &lt; $d;
        $shortest_journey{$town} = $d;
        $route{$town} = "$route{$current_town} $current_town";
    }
    my $min_distance = 1e99;   # must be larger than any possible distance
    $current_town = "";
    foreach $town (keys %unprocessed_towns) {
        next if !defined $shortest_journey{$town};
        next if $shortest_journey{$town} &gt; $min_distance ;
        $min_distance = $shortest_journey{$town};
        $current_town = $town;
    }
}

if (!defined $shortest_journey{$finish}) {
    print "No route from $start to $finish.\n";
} else {
    print "Shortest route is length = $shortest_journey{$finish}:$route{$finish} $finish.\n";
}
</pre>
</div>
</li><li>

Write a Perl function <code>printHash()</code> that displays the contents
of a Perl associative array (hash) in the format below (its the format  used by
the PHP function <code>print_r</code>) e.g.he hash table ...

<pre class="program">colours = ("John"=&gt;"blue", "Anne"=&gt;"red", "Andrew"=&gt;"green");
</pre>

and the function call ...

<pre class="program">printHash(%colours);
</pre>

should produce the output ...

<pre class="program">[Andrew] =&gt; green
[Anne] =&gt; red
[John] =&gt; blue
</pre>

Since the function achieves its effect via <code>print</code>, it doesn't
really need to return any value, but since Perl functions typically
return <em>something</em>, <code>printHash</code> should return a count
of the number of items displayed (i.e. the number of keys in the hash table).
Note that the hash should be displayed in ascending alphabetical order
on key values.

<div class="answer">
This gives the function as well as some code to test it out:


<pre class="program">#!/usr/bin/perl -w

sub printHash {
    my (%tab) = @_;
    my $n = 0;
    foreach $k (sort keys %tab) {
        print "[$k] =&gt; $tab{$k}\n";
        $n++;
    }
    return $n;
}

%h = ("David"=&gt;"green", "Phil"=&gt;"blue", "Andrew"=&gt;"red", "John"=&gt;"blue");

$nitems = printHash(%h);
print "#items = $nitems\n";
</pre>
</div>
</li><li>

A bigram is two words occuring consecutively in a piece of text.
Some pairs of words tend to occur more commonly than others as bigrams,
e.g <code>cold beer</code> or <code>programming language</code>.
<p>
Your task is to write a Perl program <code>bigrams.pl</code> which reads a piece of text,
and prints the words which occur in the text
in sorted order, one per line,  Each word should be accompanied
by the word which most frequently follows it in the text - if several
words occur equally often after the word, any of them can be printed.
The number of times the word occurs in the text should be indicated
as should the number of times the second word follows it.
Case should be ignored. For example given this text:


</p><pre class="command_line"><kbd class="shell">./bigrams.pl
</kbd><kbd>Peter Piper picked a peck of pickled peppers;
A peck of pickled peppers Peter Piper picked;
If Peter Piper picked a peck of pickled peppers,
Where's the peck of pickled peppers Peter Piper picked?
<span class="eof"></span></kbd>
<samp>a(3) peck(3)
if(1) peter(1)
of(4) pickled(4)
peck(4) of(4)
peppers(4) peter(2)
peter(4) piper(4)
picked(3) a(2)
pickled(4) peppers(4)
piper(4) picked(4)
s(1) the(1)
the(1) peck(1)
where(1) s(1)
</samp></pre>
<div class="answer">
Some notes on the  Perl solution below:
<ul>
<li> <code>\W</code> is a special Perl regexp class which matches
    any non-word character
</li><li> <code>tr/abc/def/</code> behaves like the Unix <code>tr</code>
    command
</li><li> neither of <code>tr</code>'s args is a regexp; but it supports
    <code>A-Z</code>-style ranges
</li><li> <code>bigram_count</code> is a hash where each key is a string
    and each value is a (reference to a) hash
</li></ul>
<pre class="program">#!/usr/bin/perl -w

while ($line = &lt;&gt;) {
    foreach $word (split(/\W+/, $line)) {
        $word =~ tr/A-Z/a-z/;
        $bigram_count{$last_word}{$word}++ if $last_word;
        $last_word = $word;
    }
}
foreach $first_word (sort keys %bigram_count) {
    my $most_common_second_word = "";
    my $most_common_count = 0;
    my $total_count = 0;
    foreach $second_word (sort keys %{$bigram_count{$first_word}}) {
        my $b = $bigram_count{$first_word}{$second_word};
        $total_count += $b;
        if ($b &gt; $most_common_count) {
            $most_common_second_word = $second_word;
            $most_common_count = $b;
        }
    }
    print "$first_word($total_count) $most_common_second_word($most_common_count)\n";
}
</pre>

Sample Python solution


<pre class="program">#!/usr/bin/python
import fileinput,re, collections

words = [word for line in fileinput.input() for word in re.findall(r'\w+', line.lower())]
word_counts = collections.defaultdict(int)
ngram_counts = collections.defaultdict(int)
n = 2
for ngram in [words[i:i+n] for i in range(0, len(words) - (n - 1))]:
    word_counts[ngram[0]] += 1
    ngram_counts[tuple(ngram)] += 1
print(ngram_counts)
print(word_counts)
for word in sorted(word_counts.keys()):
    following_word_counts = [(ngram_counts[ngram],ngram[1]) for ngram in list(ngram_counts.keys()) if ngram[0] == word]
    (following_word,frequency) = max(following_word_counts)
    print("%s(%s), %s(%s)" % (word,word_counts[word],following_word,frequency))
</pre>
</div>
<p></p></li></ol>
<script src="./Week 07 Tutorial_files/2041.js.download"></script>
</div>
</div>



<div id="extension-is-installed"></div></body></html>